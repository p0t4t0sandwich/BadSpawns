import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('java-library')
    id("org.spongepowered.gradle.plugin") version('2.1.1')
}

sponge {
    apiVersion(spongeapi_version)
    license("GPL-3.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin(project_id) {
        displayName(project_name)
        entrypoint("dev.neuralnexus.badspawns.sponge.SpongeBadSpawnsPlugin")
        description("A cross-API server plugin to cancel the spawns of mobs using defined regions and conditions.")
        links {
             homepage("https://github.com/p0t4t0sandwich/BadSpawns")
             source("https://github.com/p0t4t0sandwich/BadSpawns")
             issues("https://github.com/p0t4t0sandwich/BadSpawns/issues")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("taterlib") {
            version("1.0.2")
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("luckperms") {
            version("5.3.0")
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(true)
        }
    }
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // TaterLib
    compileOnly("dev.neuralnexus:TaterLib:1.16-${taterlib_version}")
}

shadowJar {
    dependencies {
        include(project(":common"))
    }

    destinationDirectory = file('../target')
    archiveFileName = "${project_name}-sponge8-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
